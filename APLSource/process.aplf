 process←{
    ⍝ Process the raw data sent to the server
     data radar←⍵
     ⍝⎕←'New Line, Processing',⍴data

     ⍝ IF multi line, but this does not happen often, unless there is some netcat repeating or buffering.
     data←(';',⎕UCS 10)(~⍤∊⍨⊆⊢)data
     {
        ⍝ Remove * (first char) from msg
         data←1↓⍵
         0::⍬⍝⎕←'Error Processing: Continuing without'
         datap←,↑((4⍴2)⊤⊢)¨{⍵⍳⍨⎕D,⎕A}data ⍝ Convert to bin
         fmt←2⊥5↑datap ⍝ First 5 bits is format of the request
         ~fmt=17:⍬ ⍝ 17 means it is ADS-B and not short Mode-S (Could implement in the future)
         fmt=17:adsbProcess datap
     }¨data
 }
