 airVelocity←{
     data←⍵
     dbr←⍺

     me←∊data[3]
     tc←∊data[4]

     me←5↓me

     st←2⊥3↑me ⍝ subtype
     me←3↓me

     ic←⊃me ⍝ intent change
     me←1↓me

     ifrcapable←⊃me ⍝ Capable of Instrument Flight Rules
     me←1↓me

     navUncertain←2⊥3↑me ⍝ Uncertainty in these numbers
     me←3↓me

     subTypeField←22↑me ⍝ The data of the subtype (differs depending on subtype)
     me←22↓me

     vrSrc←⊃me ⍝ Source of vspeed
     me←1↓me

     vrsign←1-2×⊃me
     me←1↓me

     vr←vrsign×64×(1-⍨2⊥9↑me) ⍝ Vertical speed
     me←9↓me

     me←2↓me

     saltdif←1-2×⊃me ⍝ 0, GNSS > Baro. 1, GNSS < Baro
     me←1↓me

     altdif←saltdif×25×(1-⍨2⊥7↑me) ⍝ Difference between GNSS and Baro

     dbr←(dbr subTypeField){⍵>2:⍵ ##.calcAirSpeed ⍺ ⋄ ⍵ ##.calcGroundSpeed ⍺}st

     dbr[22]←ifrcapable

     dbr[23]←vr
     dbr[24]←altdif

     dbr data
 }
